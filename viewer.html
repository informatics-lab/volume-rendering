<!DOCTYPE html>
<html>
<head lang="en">
    <meta charset="UTF-8">
    <title></title>
</head>
<body>

<script src="./three.min.js"></script>
<script type="text/javascript" src=	"./TrackballControls.js"></script>

<!-- first pass shaders -->

<script id="vertexShaderFirstPass" type="x-shader/x-vertex">
    varying vec3 worldSpaceCoords;

    vec3 toLocal(vec3 p) {
  		return p + vec3(0.5);
	}

    void main(){
        //Set the world space coordinates of the back faces vertices as output.
        worldSpaceCoords = toLocal(position); //move it from [-0.5;0.5] to [0,1]
        gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
    }
</script>

<script id="fragmentShaderFirstPass" type="x-shader/x-fragment">
    varying vec3 worldSpaceCoords;
    void main(){
        //The fragment's world space coordinates as fragment output.
        gl_FragColor = vec4( worldSpaceCoords.x , worldSpaceCoords.y, worldSpaceCoords.z, 1 );
    }
</script>


<!-- second pass shaders -->
<script id="vertexShaderSecondPass" type="x-shader/x-vertex">
	varying vec3 worldSpaceCoords;
	varying vec4 projectedCoords;

	vec3 toLocal(vec3 p) {
  		return p + vec3(0.5);
	}
	
	void main()
	{
		worldSpaceCoords = (modelMatrix * vec4(toLocal(position), 1.0 )).xyz;
		gl_Position = projectionMatrix *  modelViewMatrix * vec4( position, 1.0 );
		projectedCoords =  projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
	}
</script>

<script id="fragmentShaderSecondPass" type="x-shader/x-fragment">
    varying vec3 worldSpaceCoords;
    varying vec4 projectedCoords;

    uniform sampler2D firstPassTexture, dataTexture; //i.e. tex and cubeTex
    uniform float steps;
    uniform float alphaCorrection;

    uniform vec3 dataShape;
    uniform vec2 textureShape;

    const int MAX_STEPS = 512;
    const float MIN_TRANS = 0.0;

    vec3 toLocal(vec3 p) {
    	// changes from clip coords (-0.5 -> 0.5) to local coords(0->1)
  		return p + vec3(0.5);
	}

    vec3 getDatumColor(float datum) {
    	vec3 color = vec3(255, 255, 255);
    	if (datum == 9999.9999){
    		color = vec3(255, 0, 0);
    	}
    	return color;
    }

    float getDatumAlpha(float datum) {
    	return datum * alphaCorrection;
    }

    float getDatum(sampler2D tex, vec3 pos, vec3 dataShape, vec2 nTiles, float tilesPerLayer, vec2 tileDim, float thisTileN){
        float zTile = floor(thisTileN/tilesPerLayer);
        float yTile = floor((thisTileN - (zTile * tilesPerLayer)) / nTiles.x);
        float xTile = mod((thisTileN - (zTile * tilesPerLayer)), nTiles.x);
        
        vec2 thisPoint = vec2(xTile+pos.x, yTile+pos.y) * tileDim;

        float datum;
        if (zTile == 0.0){
            datum = texture2D(tex, thisPoint).r;
        }else if (zTile == 1.0){
            datum = texture2D(tex, thisPoint).g;
        }else if (zTile == 2.0){
            datum = texture2D(tex, thisPoint).b;
        }

        return datum;
    }
 
    float sampleAs3DTexture(sampler2D tex, vec3 pos, vec3 dataShape, vec2 texShape) {
    	/* 
		A function to reference a 2D RGBA texture which contains tiles 3D array data.

		Tiling goes column, row, channel

		Args:
			* tex: texture of tiled data
			* pos: position of the datum
			* dataShape: the x,y,z shape of the data which has been tiled
			* texShape: the x,y dims of the tiles texture
    	*/

        vec2 fracNTiles = texShape.xy / dataShape.xy;
        vec2 nTiles = vec2(floor(fracNTiles.x), floor(fracNTiles.y));
        float tilesPerLayer = nTiles.x * nTiles.y;
        vec2 tileDim = vec2(1.0, 1.0) / fracNTiles;
        float thisTileN = floor((dataShape.z-1.0) * pos.z);
        float thisTileNp1 = min(thisTileN+1.0, dataShape.z);

    	float datumN = getDatum(tex, pos, dataShape, nTiles, tilesPerLayer, tileDim, thisTileN);
        float datumNp1 = getDatum(tex, pos, dataShape, nTiles, tilesPerLayer, tileDim, thisTileNp1);

    	float zDiff = mod((dataShape.z-1.0) * pos.z, 1.0);

 	  	return ((1.0 - zDiff) * datumN) + (zDiff * datumNp1);
    }

    vec4 getRGBAfromDataTex(sampler2D tex, vec3 pos, vec3 dataShape, vec2 texShape){
    	float datum = sampleAs3DTexture(tex, pos, dataShape, texShape);
    	vec3 color = getDatumColor(datum);
    	float alpha = getDatumAlpha(datum);

    	return vec4(color.xyz, alpha);
    }

    vec4 getPathRGBA(vec3 startPos, vec3 endPos, float steps, sampler2D tex){
    	/* Calculates the total RGBA values of a given path through a texture */

    	//The direction from the front position to back position.
	    vec3 dir = endPos - startPos;
	
	    float rayLength = length(dir);
	
	    //Calculate how long to increment in each step.
	    float delta = 1.0 / steps;

	    //The increment in each direction for each step.
	    vec3 deltaDirection = normalize(dir) * delta;
	    float deltaDirectionLength = length(deltaDirection);

	    vec3 currentPosition = startPos;

    	//The color accumulator.
	    vec3 accumulatedColor = vec3(0.0);
	
        // the transmission of this data point
        float thisTransmission;

	    //The amount of transmission remaining.
	    float remainingTransmission = 1.0;
	
	    //How long has the ray travelled so far.
	    float accumulatedLength = 0.0;
	
	    //vec4 dataSample;
	    vec4 dataSample;

	    //Perform the ray marching iterations
	    for(int i = 0; i < MAX_STEPS; i++){
		    //Get the voxel intensity value from the 3D texture.	
		    dataSample = getRGBAfromDataTex(dataTexture, currentPosition, dataShape, textureShape);
			
            // get the transmission of this data point from the alpha value
            thisTransmission = exp( -alphaCorrection*delta*dataSample.a );
		    
            //Calculate the amount of light remaining.
		    remainingTransmission *= thisTransmission;

		    //Perform the composition.
		    accumulatedColor += (1.0 - thisTransmission) * dataSample.xyz * remainingTransmission;
			//accumulatedColor += dataSample;
		
		    //Advance the ray.
		    currentPosition += deltaDirection;
		    accumulatedLength += deltaDirectionLength;
				      
		    //If the length traversed is more than the ray length, or if the transmission < limit (no light left) then break
		    if(accumulatedLength >= rayLength || remainingTransmission <= MIN_TRANS ){
				break;
			}
	    }

	    float alpha = (1.0 - remainingTransmission);
	    return vec4(accumulatedColor.xyz/alpha, alpha);
    }

    // max 2d size is 4096 x 4096

    void main( void ) {
	    //Transform the coordinates it from [-1;1] to [0;1]
	    vec2 firstPassTexCoord = vec2(((projectedCoords.x / projectedCoords.w) + 1.0 ) / 2.0,
					    ((projectedCoords.y / projectedCoords.w) + 1.0 ) / 2.0 );

	    //The back position is the world space position stored in the texture.
	    vec3 backPos = texture2D(firstPassTexture, firstPassTexCoord).xyz;

	    //The front position is the world space position of the second render pass.
	    vec3 frontPos = worldSpaceCoords;
	
		// cast ray from front position in direction of back position
	    gl_FragColor = getPathRGBA(frontPos, backPos, steps, dataTexture);
    }

</script>


<script src="viewer.js"></script>
</body>
</html>

